apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main { res.srcDirs = ['src/main/res', 'src/test/main/res', 'src/test/resources'] }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'

    implementation 'com.amazonaws:aws-android-sdk-core:[2.16,)'
    implementation 'com.amazonaws:aws-android-sdk-appsync:[3.0,)'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.16.0'
    testImplementation 'org.json:json:20190722'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'com.sudoplatform:sudouser:7.0.1'
    api 'com.sudoplatform:sudoconfigmanager:1.2.2'
    api 'com.sudoplatform:sudouser:7.0.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

apply plugin: 'maven'

ext {
    PUBLISH_GROUP_ID = 'com.sudoplatform'
    PUBLISH_ARTIFACT_ID = 'sudoapiclient'
    PUBLISH_DESCRIPTION = 'An android library used by SudoPlatform SDKs for sharing a single AWS AppSync Client'
    PUBLISH_PROJECT_URL = 'https://github.com/sudoplatform/sudo-api-client-android'
    PUBLISH_PROJECT_SCM = 'scm:git:github.com/sudoplatform/sudo-api-client-android.git'
    if (project.hasProperty('tag')) {
        PUBLISH_VERSION = "$tag"
    } else {
        PUBLISH_VERSION = "0.0.0"
    }
}

apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"

